QOAUTH_INCDIR = $$[QT_INSTALL_HEADERS]
QOAUTH_LIBDIR = $$[QT_INSTALL_LIBS]

QOAUTH_INCDIR ~= s!/qt4!!
QOAUTH_LIBDIR ~= s!/qt4!!

CONFIG *= qt
CONFIG += crypto

# if we are including oauth.prf from the qoauth tree (and not utilizing it as
# an installed qmake CONFIG feature), then point to the tree.  this allows our
# qoauth tree apps to build before qoauth itself is installed.
exists($$PWD/qoauth.pro) {
    QOAUTH_INCDIR = $$PWD/include
    QOAUTH_LIBDIR = $$PWD/lib
}

LINKAGE =

# on mac, if qoauth was built as a framework, link against it
mac: {
    framework_dir = $${QOAUTH_LIBDIR}
    exists($$framework_dir/qoauth.framework) {
        LIBS += -F$$framework_dir
        exists($$PWD/qoauth.pro): INCLUDEPATH += $$QOAUTH_INCDIR
        else: INCLUDEPATH += $$framework_dir/qoauth.framework/Headers
        LINKAGE = -framework qoauth
    }
}

# else, link normally
isEmpty(LINKAGE) {
    exists($$PWD/qoauth.pro): INCLUDEPATH += $$QOAUTH_INCDIR
    else: INCLUDEPATH += $$QOAUTH_INCDIR/QtOAuth
    LIBS += -L$$QOAUTH_LIBDIR
    LINKAGE = -lqoauth
    CONFIG(debug, debug|release) {
        windows:LINKAGE = -lqoauthd
        mac:LINKAGE = -lqoauth_debug
    }
}

LIBS += $$LINKAGE
